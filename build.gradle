ext {
    configuration = [
            versionCode: 1,
            versionName: "0.1.0"
    ]

    bintrayConfig = [
            userOrg         : "blipinsk",
            userName        : "Bartosz Lipinski",
            userEmail       : "bartoszlipinski90@gmail.com",
            groupId         : "com.bartoszlipinski.dagger2-ktx",
            name            : "dagger2-ktx",
            artifactLibrary : "dagger2-ktx",
            artifactCompiler: "dagger2-ktx-compiler",
            description     : "",
            website         : "https://github.com/blipinsk/dagger2-ktx",
            gitUrl          : "https://github.com/blipinsk/dagger2-ktx.git",
            licenses        : ["Apache-2.0"],
            licenseDesc     : "The Apache Software License, Version 2.0"
    ]

    bintrayUser = BINTRAY_USERNAME
    bintrayKey = BINTRAY_API_KEY
    dryRun = 'true'
    isUploading = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}

buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha15'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.8.1'
        // clean build bintrayUpload
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    group = bintrayConfig.groupId
    version = configuration.versionName

    if (isUploading && project.name in [bintrayConfig.artifactLibrary, bintrayConfig.artifactCompiler]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                println 'Overriding pom-file to make sure we can sync to maven central!'
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name "$project.name"
                        artifactId project.name
                        packaging project.name == bintrayConfig.artifactCompiler ? 'jar' : 'aar'
                        description bintrayConfig.description
                        url bintrayConfig.website
                        version configuration.versionName

                        scm {
                            url bintrayConfig.gitUrl
                            connection bintrayConfig.gitUrl
                            developerConnection bintrayConfig.gitUrl
                        }

                        licenses {
                            license {
                                name bintrayConfig.licenseDesc
                            }
                        }

                        developers {
                            developer {
                                id bintrayConfig.userOrg
                                name bintrayConfig.userName
                                email bintrayConfig.userEmail
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}